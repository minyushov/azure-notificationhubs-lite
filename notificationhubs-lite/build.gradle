apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
  buildToolsVersion androidBuildToolsVersion
  compileSdkVersion androidCompileSdkVersion

  defaultConfig {
    minSdkVersion androidMinSdkVersion
    targetSdkVersion androidTargetSdkVersion
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = "11"
  }

  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"

  implementation "com.squareup.okio:okio:3.0.0"
  implementation "com.squareup.okhttp3:okhttp:4.9.3"

  testImplementation "androidx.test.ext:junit-ktx:1.1.3"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
  testImplementation "org.robolectric:robolectric:4.7.3"
  testImplementation "com.squareup.okhttp3:mockwebserver:4.9.3"
}

afterEvaluate {
  def properties = new Properties()
  def file = rootProject.file("local.properties")
  if (file.isFile()) {
    file.withInputStream { stream ->
      properties.load(stream)
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        groupId "io.github.minyushov"
        artifactId = "azure-notification-hubs"
        version = "2.0.0"

        from(components.release)

        pom {
          name = artifactId
          description = "Simple Azure Notification Hubs SDK for Android"
          url = "https://github.com/minyushov/azure-notificationhubs-lite"
          developers {
            developer {
              id = "minyushov"
              name = "Semyon Minyushov"
              email = "minyushov@gmail.com"
            }
          }
          licenses {
            license {
              name = "Apache-2.0"
              url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          scm {
            connection = "https://github.com/minyushov/azure-notificationhubs-lite.git"
            developerConnection = "https://github.com/minyushov/azure-notificationhubs-lite.git"
            url = "https://github.com/minyushov/azure-notificationhubs-lite/tree/master"
          }
        }
      }
    }
    repositories {
      maven {
        name "sonatype"
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username properties["SONATYPE_USER"]
          password properties["SONATYPE_PASSWORD"]
        }
      }
    }
  }

  signing {
    useInMemoryPgpKeys(
      properties["GPG_KEY_ID"],
      properties["GPG_KEY"],
      properties["GPG_PASSWORD"]
    )
    sign(publishing.publications)
  }
}